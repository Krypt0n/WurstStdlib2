package ChannelAbilityPreset
import NoWurst
import AbilityObjEditing
import OrderStringFactory
import BitSet

public enum SpellOption
	VISIBLE
	TARGETIMAGE
	PHYSICALSPELL
	UNIVERSALSPELL
	UNIQUECAST

public enum Targettype
	NONE
	UNIT
	POINT
	POINTUNIT
	PASSIVE
	INSTANT

public function Targettype.toString() returns string
	switch this
		case NONE
			return ""
		case Targettype.POINT
			return "Point Target"
		case Targettype.UNIT
			return "Unit Target"
		case Targettype.POINTUNIT
			return "Point & Unit Target"
		case PASSIVE
			return "Passive"
		case INSTANT
			return "Instant Cast"

public class ChannelAbilityPreset extends AbilityDefinitionIllidanChannel
	private bitset optionSet = bitset(0)
	private string orderString

	function getOrderString() returns string
		return orderString

	construct(int newId, int lvls, boolean removeChannelProperties, TooltipGenerator tgen)
		super(newId)
		registerTooltipGenerator(tgen)
		setup(lvls, removeChannelProperties)

	construct(int newId, int lvls, boolean removeChannelProperties)
		super(newId)
		setup(lvls, removeChannelProperties)

	function setup(int lvls, boolean removeChannelProperties)
		this.lvls = lvls
		tooltipStartListen()
		setLevels(lvls)
		tooltipStopListen(false)
		orderString = orderStringFactory.getOrderString(true)
		presetBaseOrderID(_lvl -> orderString)
		if removeChannelProperties
			removeChannelProperties(true)

	function makeUnitSpell(int mana, real cooldown)
		setHeroAbility(false)
		presetManaCost(lvl -> mana)
		presetCooldown(lvl -> cooldown)

	function removeChannelProperties(boolean removeVisuals)
		removeChannelProperties(removeVisuals, true)

	function removeChannelProperties(boolean removeVisuals, boolean makeVisible)
		presetDisableOtherAbilities(lvl -> false)
		presetFollowThroughTime(lvl -> 0.)
		presetArtDuration(lvl -> 0.)
		if makeVisible
			presetOption(SpellOption.VISIBLE, true)
		if removeVisuals
			setArtCaster("")
			setArtEffect("")
			setArtSpecial("")
			setArtTarget("")

	function presetTargetTypes(Targettype ttype)
		presetTargetType(lvl -> ttype castTo int)

	function presetOption(SpellOption opt, boolean flag)
		let pow = opt castTo int
		if optionSet.contains(pow)
			if not flag
				optionSet = optionSet.remove(pow)
		else if flag
			optionSet = optionSet.add(pow)

		presetOptions(lvl -> optionSet.val)

	function hasOption(SpellOption option) returns boolean
		return optionSet.contains(option castTo int)
